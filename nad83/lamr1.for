C     SccsID = "$Id: lamr1.for 54965 2011-06-12 01:03:11Z Srinivas.Reddy $"  
      SUBROUTINE LAMR1(NORTH,EAST,LAT,LON,CM,EO,NB,SINFO,RB,K,
     &                  ER,ESQ,CONV,KP)
*** LAMBERT CONFORMAL CONIC PROJECTION, 2 STD PARALLELS
*** CONVERSION OF GRID COORDINATES TO GEODETIC COORDINATES
*** REVISED SUBROUTINE OF T. VINCENTY -- FEB.25, 1985
************** SYMBOLS AND DEFINITIONS ********************
*** LATITUDE POSITIVE NORTH, LONGITUDE POSITIVE WEST.  ALL
***          ANGLES ARE IN RADIAN MEASURE.
*** FI,LAM ARE LAT. AND LONG. RESPECTIVELY
*** NORTH,EAST ARE NORTHING AND EASTING COORDINATES RESPECTIVELY
*** CONV IS CONVERGENCE
*** KP IS POINT SCALE FACTOR
*** ER IS THE SEMI-MAJOR AXIS FOR GRS-80
*** ESQ IS THE SQUARE OF THE 1ST ECCENTRICITY
*** E IS THE 1ST ECCENTRICITY
*** CM IS THE CENTRAL MERIDIAN OF THE PROJECTION ZONE
*** EO IS THE FALSE EASTING VALUE AT THE CM
*** NB IS THE FALSE NORTHING FOR THE SOUTHERNMOST
***       PARALLEL OF THE PROJECTION ZONE
*** SINFO = SIN(FO)=> WHERE FO IS THE CENTRAL PARALLEL
*** RB IS THE MAPPING RADIUS AT THE SOUTHERNMOST PARALLEL
*** K IS MAPPING RADIUS AT THE EQUATOR
*************************************************************

      IMPLICIT DOUBLE PRECISION(A-H,K-Z)

      E=DSQRT(ESQ)
      NPR=RB-NORTH+NB
      EPR=EAST-EO
      GAM=DATAN(EPR/NPR)
      LON=CM-(GAM/SINFO)
      RPT=DSQRT(NPR*NPR+EPR*EPR)
      Q=DLOG(K/RPT)/SINFO
      TEMP=DEXP(Q+Q)
      SINE=(TEMP-1.D0)/(TEMP+1.D0)

      DO 10 I=1,3
      F1=(DLOG((1.D0+SINE)/(1.D0-SINE))-E*DLOG((1.D0+E*SINE)
     &   /(1.D0-E*SINE)))/2.D0-Q
      F2=1.D0/(1.D0-SINE*SINE)-ESQ/(1.D0-ESQ*SINE*SINE)
 10   SINE=SINE-F1/F2
      LAT=DASIN(SINE)
*********************************************************************
C
      FI = LAT
      LAM = LON
      SINLAT=SIN(FI)
      COSLAT=COS(FI)
      CONV=(CM-LAM)*SINFO
C
      Q=(LOG((1+SINLAT)/(1-SINLAT))-E*LOG((1+E*SINLAT)/(1-E*SINLAT)))/2.
      RPT=K/EXP(SINFO*Q)
      WP=SQRT(1.-ESQ*SINLAT**2)
      KP=WP*SINFO*RPT/(ER*COSLAT)

      RETURN
      END
